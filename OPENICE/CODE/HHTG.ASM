	.MLIB	"HHMACS.LIB"
	.FILE	"HHTG.ASM"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

*
*GET THE SYSTEM STUFF
*
	.INCLUDE	"HH.INC"
	.INCLUDE	"HHSTRING.H"
	.INCLUDE	"IMGTBL.GLO"
	.INCLUDE	"HHTG.TBL"
	.INCLUDE	"HHTG2.TBL"
	.include	"hhtg3.tbl"
	.include	"hhtg.e"
	.include	"hhsnd.g"
	.include	"hhc.g"
	.include	"hhd.g"
	.include	"hhutil.g"
	.include	"hhpall.g"
	.include	"hhgame.g"
	.include	"hhfade.g"
	.include	"hhtext.g"
	.include	"hhproc.g"
	.include	"hhafunc.g"
	.include	"hhmisc.g"
	.include	"hhram.g"
	.include	"hhpick.g"
	.include	"hhstable.g"
	.include	"hhspeech.g"
	.include	"hhtimer.g"
	.include	"hhpup.g"

; end of include files

	.BSS	TG_FLAG,16
	.BSS	SLIDE_OFF_COUNT,16

	.TEXT
	.EVEN

**************************************************************************
*								         *
* TONIGHTS_GAME - DISPLAY TEAM #1 VS TEAM #2 MESSAGE			 *
* 									 *
* PASS:									 *
* NUTIN'								 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

CENTER_LIGHT_OBJ	EQU	PDATA		;UHL
NHL_LOGO_OBJ		EQU	PDATA+020H	;UHL
LEFT_HELMET_OBJ		EQU	PDATA+040H	;UHL
RGHT_HELMET_OBJ		EQU	PDATA+060H	;UHL

PLACK_Y			EQU	191 ;178

PLACK_Y_OFFSCRN		EQU	195

NHL_LOGO_Y		EQU	120

TONIGHTS_GAME
	calla	MY_WIPEOUT

	BLACKON					;USE BLACKPAL

	MOVI	FONTGRYP,A0
	CALLA	GETFPAL
	MOVI	40,A0
;	MOVI	20,A0
	CALLA	set_tune_a0

	MOVI	ININTRO_TG,A14
	MOVE	A14,@GAME_STATE,W

	CLR	A14
	MOVE	A14,@TG_FLAG,W

	MOVI	TONIGHTS_GAME_MESS,A8
	JSRP	LM_PRINTF

	MOVE	@team1,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	TEAM_PLACK_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y,0-PLACK_Y_OFFSCRN],A1
	MOVK	2,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVI	BLUFRMP,A0
	CALLA	CHANGE_PAL
	CLR	A14
	MOVE	A14,*A8(ODATA),W
	MOVI	LEFT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y+4,60-PLACK_Y_OFFSCRN],A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL
	MOVI	60,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	LEFT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	[PLACK_Y+32,105-PLACK_Y_OFFSCRN],A5
	MOVI	[PLACK_Y+44,105-PLACK_Y_OFFSCRN],A6
	MOVE	@team1,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	TG_LEFT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
TG_LEFT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVI	105,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	LEFT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVI	105,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	LEFT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	BIG_GHELMET_INIT,B0
	CALLA	EASYMAKE
	MOVI	[100+213,99],A1
	MOVI	3,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A9(TD_BIG_GHELM_PAL),A0,L
	CALLA	CHANGE_PAL
	MOVE	A8,*A13(LEFT_HELMET_OBJ),L

	MOVE	@team2,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	TEAM_PLACK_INIT,B0
	CALLA	EASYMAKE
	MOVE	*A8(OCTRL),A14,W
	ORI	M_FLIPH,A14
	MOVE	A14,*A8(OCTRL),W
	MOVI	[PLACK_Y,399+PLACK_Y_OFFSCRN],A1
	MOVK	2,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVI	REDFRMP,A0
	CALLA	CHANGE_PAL
	MOVI	399,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	RIGHT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y+4,339+PLACK_Y_OFFSCRN],A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL
	MOVI	339,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	RIGHT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	[PLACK_Y+32,301+PLACK_Y_OFFSCRN],A5
	MOVI	[PLACK_Y+44,301+PLACK_Y_OFFSCRN],A6
	MOVE	@team2,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	TG_RIGHT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
TG_RIGHT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVI	301,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	RIGHT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVI	301,A14
	MOVE	*A8(OANIOFFX),A0,W
	SUB	A0,A14
	MOVE	A14,*A8(ODATA),W
	MOVI	RIGHT_PLAQ_SLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVI	BIG_GHELMET_INIT,B0
	CALLA	EASYMAKE
	MOVE	*A8(OCTRL),A14,W
	ORI	M_FLIPH,A14
	MOVE	A14,*A8(OCTRL),W
	MOVI	[100+213,299],A1
	MOVI	3,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A9(TD_BIG_GHELM_PAL),A0,L
	CALLA	CHANGE_PAL
	MOVE	A8,*A13(RGHT_HELMET_OBJ),L

	MOVI	CENTER_LIGHT_INIT,B0
	CALLA	MULTIMAKE
	MOVI	[NHL_LOGO_Y,199],A1			;UNFLIPPED POSITION
	MOVI	[1,1],A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	A8,*A13(CENTER_LIGHT_OBJ),L

	MOVI	GROUP_LIGHTS_INIT,B0
	CALLA	EASYMAKE
	MOVI	[50,70],A1
	MOVI	[1,0],A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ

	MOVI	GROUP_LIGHTS_INIT,B0
	CALLA	EASYMAKE
	MOVE	*A8(OCTRL),A14,W
	ORI	M_FLIPH,A14
	MOVE	A14,*A8(OCTRL),W
	MOVI	[50,399-70],A1
	MOVI	[1,0],A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ

	MOVI	NHL_LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVE	A8,*A13(NHL_LOGO_OBJ),L

	BLACKOFF

	SLEEP	1

	PUSH	A9
	CALLA	START_TG_POWERUP_PROC
	PULLQ	A9

	CLR	A0
	CALLA	FADEINF			;FADE IN ALL PALETTES
	JSRP	FADE_WAIT
	calla	spx_tonights_matchup
	
	MOVE	*A13(RGHT_HELMET_OBJ),A8,L
;	MOVI	[-8,0],A14
	MOVI	[-14,0],A14
	MOVE	A14,*A8(OYVEL),L
	MOVI	HELM_YSLIDE_ANIM,A1
	CALLA	STRT_ANIM

	MOVE	*A13(LEFT_HELMET_OBJ),A8,L
;	MOVI	[-8,0],A14
	MOVI	[-14,0],A14
	MOVE	A14,*A8(OYVEL),L
	MOVI	HELM_YSLIDE_ANIM,A1
	CALLA	STRT_ANIM

	CLR	A11
TG_HELM_SLIDE_WAIT
	SLEEP	1
	CALLR	CHECK_BUTTON_ABORT
	MOVE	@TG_FLAG,A14,W
	JRZ	TG_HELM_SLIDE_WAIT

TG_PLAQ_SLIDE_WAIT
	SLEEP	1
	CALLR	CHECK_BUTTON_ABORT
	MOVE	@TG_FLAG,A14,W
	JRNZ	TG_PLAQ_SLIDE_WAIT

	MOVE	A11,A11
	JRNZ	TG_SLAM

	MOVK	30,A8
TG_PRE_SPEECH_WAIT
	SLEEP	1
	CALLR	CHECK_BUTTON_ABORT
	JRNZ	TG_SLAM
	DSJS	A8,TG_PRE_SPEECH_WAIT

;	MOVI	0A9DH,A4
;	CALLA	SEND_RAW_SOUND
;	calla	spx_tonights_matchup

	MOVK	30,A8
TG_PRE_TEAM_SPEECH_WAIT
	SLEEP	1
	CALLR	CHECK_BUTTON_ABORT
	JRNZ	TG_SLAM
	DSJS	A8,TG_PRE_TEAM_SPEECH_WAIT

TG_SLAM
	MOVI	S_HELMETS_STARTUP,A4
	CALLA	do_sound
	MOVE	A4,A9

	MOVE	*A13(NHL_LOGO_OBJ),A8,L
	MOVI	NHL_LOGO_SPIN_ANIM,A1
	CALLA	STRT_ANIM

	MOVE	*A13(RGHT_HELMET_OBJ),A8,L
	MOVI	279,A14 				;259+39
	MOVE	A14,*A8(ODATA),W
	MOVI	RIGHT_HELM_ANIM,A1
	CALLA	STRT_ANIM

	MOVE	*A13(LEFT_HELMET_OBJ),A8,L
	MOVI	118,A14					;138-39
	MOVE	A14,*A8(ODATA),W
	MOVI	LEFT_HELM_ANIM,A1
	CALLA	STRT_ANIM

	MOVE	*A13(CENTER_LIGHT_OBJ),A8,L
	MOVI	CENTER_LIGHT_DIM_ANIM,A1
TG_CENTER_LIGHT_LUPE
	CALLA	STRT_ANIM
	MOVE	*A8(OPARTS),A8,L
	JRNZ	TG_CENTER_LIGHT_LUPE			;BR=MORE PARTS

	MOVK	2,A14
	MOVE	A14,@TG_FLAG,W
TG_SLAM_WAIT
	SLEEP	1
	MOVE	@TG_FLAG,A14,W
	JRNZ	TG_SLAM_WAIT

	MOVE	A9,A0
	CALLA	cancel_track_sound
	
	MOVI	S_EXPLO_4,A4
	CALLA	do_sound

	MOVI	EXPLO_INIT,B0
	CALLA	EASYMAKE

	MOVE	*A13(NHL_LOGO_OBJ),A8,L
	MOVI	NHL_DEL_ANIM,A1
	CALLA	STRT_ANIM

	SLEEP	2

	MOVI	FRAG1_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG2_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG3_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	SLEEP	1
	MOVI	FRAG3_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG4_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG4_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	SLEEP	1
	MOVI	FRAG4_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG4_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	MOVI	FRAG4_ANIM,A9
	CREATE	PID_IND,NHL_LOGO_FRAG_PROC
	SLEEP	1

	SLEEP	15

	MOVI	99,A14
	MOVE	A14,@MAX_PALXFERS,W

	MOVK	3,A9
	CREATE	PID_SKY,SKYWHITE
	SLEEP	1
	MOVI	TG_NOWHITE_LIST,A0
	CALLA	GOWHITE
	JSRP	FADE_WAIT
	CALLA	RESET_MAX_PALXFERS
	calla	kill_all_animations
	RETP

TG_NOWHITE_LIST
	.LONG	FRAG01P,0
;	.LONG	BGBRSTP,FRAG01P,0

**************************************************************************
*								         *
* CHECK_BUTTON_ABORT - INCREMENT ABORT COUNT IF ACTIVE PLAYER'S BUTTON	 *
* 		       PRESSED						 *
* 									 *
* PASS:									 *
* A11 = CURRENT ABORT COUNT						 *
* RETURN:								 *
* A11 = UPDATED ABORT COUNT						 *
* IF ABORT THEN Z SET, ELSE Z CLR					 *
*								         *
**************************************************************************

CHECK_BUTTON_ABORT
	MOVE	@PLAYER_BITS,A0,W

	MOVE	@GAME_STATE,A14,W
	CMPI	INEPILOG,A14
	JRNE	CBA_GO				;BR=PLAYER_BITS OKAY
	MOVE	@LAST_PLAYER_BITS,A0,W
CBA_GO
	SLL	5,A0

	MOVI	bw_tab,A1
	KITN	4
	JREQ	CBA_ADD_TABLE			;BR=4 PLAYER KIT
	MOVI	bw_tab_2P,A1			;2 PLAYER KIT
CBA_ADD_TABLE
	ADD	A1,A0
	MOVE	*A0,A14,L
	MOVE	@SWITCH,A0,L
	NOT	A0
	AND	A0,A14
	JRZ	CBA_DONE			;BR=NO ACTIVE PLAYER'S BUTTON
	INC	A11				;ABORT!
CBA_DONE
	RETS
	
**************************************************************************
*								         *
* NHL_LOGO_FRAG_PROC							 *
*								         *
**************************************************************************

NHL_LOGO_FRAG_PROC
	MOVI	FRAG_INIT,B0
	CALLA	EASYMAKE

	MOVK	4,A0
	CALLA	RAND0
	SLL	4,A0
	MOVE	*A8(OCTRL),A14,W
	OR	A0,A14
	MOVE	A14,*A8(OCTRL),W

	MOVI	199+33,A0
	MOVI	199-33,A1
	CALLA	RANGRAND
	MOVE	A0,A2

	MOVI	NHL_LOGO_Y+27,A0
	MOVI	NHL_LOGO_Y-27,A1
	CALLA	RANGRAND
	SLL	16,A0
	MOVY	A0,A1
	MOVX	A2,A1

	MOVI	TEXTZVAL-1,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ

	MOVI	[6,0],A0
	MOVI	[4,0],A1
	CALLA	RANGRAND
	MOVE	A0,A1
	MOVI	512,A0
	CALLA	RANDPER
	JRC	NLFP_XSET
	NEG	A1
NLFP_XSET
	MOVE	A1,*A8(OXVEL),L

	MOVI	[6,0],A0
	MOVI	[4,0],A1
	CALLA	RANGRAND
	MOVE	A0,A1
	MOVI	512,A0
	CALLA	RANDPER
	JRC	NLFP_YSET
	NEG	A1
NLFP_YSET
	MOVE	A1,*A8(OYVEL),L

	MOVE	A9,A1
	CALLA	STRT_ANIM

	MOVK	10,A0		
	CALLA	RAND0
	MOVE	A0,A14
	SLL	4,A0
	SLL	5,A14
	ADD	A14,A0
	MOVE	*A8(ANIMSCR),A14,L
	ADD	A0,A14
	MOVE	A14,*A8(ANIMFRM),L

	DIE


FRAG_INIT
	.long	FRAG401
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE
	.word	OM_OID
	.word	OID_SELECT_1

FRAG4_ANIM
	LW	FRAG401,2
	LW	FRAG402,2
	LW	FRAG403,2
	LW	FRAG404,2
	LW	FRAG405,2
	LW	FRAG406,2
	LW	FRAG407,2
	LW	FRAG408,2
	LW	FRAG409,2
	LW	FRAG410,2
	.long	0
	
FRAG3_ANIM
	LW	FRAG301,2
	LW	FRAG302,2
	LW	FRAG303,2
	LW	FRAG304,2
	LW	FRAG305,2
	LW	FRAG306,2
	LW	FRAG307,2
	LW	FRAG308,2
	LW	FRAG309,2
	LW	FRAG310,2
	.long	0

FRAG2_ANIM
	LW	FRAG201,2
	LW	FRAG202,2
	LW	FRAG203,2
	LW	FRAG204,2
	LW	FRAG205,2
	LW	FRAG206,2
	LW	FRAG207,2
	LW	FRAG208,2
	LW	FRAG209,2
	LW	FRAG210,2
	.long	0

FRAG1_ANIM
	LW	FRAG101,2
	LW	FRAG102,2
	LW	FRAG103,2
	LW	FRAG104,2
	LW	FRAG105,2
	LW	FRAG106,2
	LW	FRAG107,2
	LW	FRAG108,2
	LW	FRAG109,2
	LW	FRAG110,2
	.long	0

**************************************************************************
*								         *
* A_TG_FLAG_WAIT - WAIT FOR TG_FLAG TRUE				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_TG_FLAG_WAIT
	MOVE	@TG_FLAG,A14,W
	JRNZ	ATGFW_DONE			;BR=TRUE

	MOVE	*A8(ANIMFRM),A14,L		;HOLD FRAME
	SUBI	020H+010H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L

ATGFW_DONE
	RETS

**************************************************************************
*								         *
* A_YSLIDE_WAIT - WAIT FOR HELMET IN Y POSITION				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_YSLIDE_WAIT
	MOVE	*A8(OYPOS),A14,W
	CMPI	62,A14
;	JRGT	AHW_STAY			;BR=NOT IN POSITION
	JRLE	AHW_STOP			;BR=IN POSITION

	MOVE	*A8(OYPOS),A14,W
;	CMPI	158,A14
;	JRGT	AHW_STAY			;BR=NOT HALFWAY
;	CMPI	110,A14
;	JRGT	AHW_STAY			;BR=NOT 3/4
	CMPI	167,A14
	JRGT	AHW_STAY			;BR=NOT YET

	MOVK	10,A14
	MOVE	A14,@TG_FLAG,W			;FLAG PLAQS TO COME IN
	JRUC	AHW_STAY

AHW_STOP
	MOVI	62,A14
	MOVE	A14,*A8(OYPOS),W

	CLR	A14
	MOVE	A14,*A8(OYVEL),L

	RETS

	JAUC	PULL_ANIM
AHW_STAY
	MOVE	*A8(OYVEL),A14,L
	ADDI	08860H,A14
	JRNN	AHW_STOP			;BR=NEVER GONNA GET THERE
	MOVE	A14,*A8(OYVEL),L

	MOVE	*A8(ANIMFRM),A14,L		;HOLD FRAME
	SUBI	020H+010H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
	RETS

**************************************************************************
*								         *
* A_YSLIDE_DONE - HELMET FINISHED THIS SEQUENCE				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_YSLIDE_DONE
	CLR	A14
	MOVE	A14,*A8(OXVEL),L

	MOVE	@TG_FLAG,A14,W
	DEC	A14
	MOVE	A14,@TG_FLAG,W

	JAUC	PULL_ANIM
	
**************************************************************************
*								         *
* A_ACCEL_LEFT								 *
* A_ACCEL_RIGHT - ACCELERATE HELMET X VELOCITY				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_ACCEL_LEFT
	MOVE	*A8(OYVEL),A14,L
;	ADDI	-04000H,A14
	MOVE	A14,*A8(OYVEL),L

	MOVE	*A8(OXVEL),A14,L
	ADDI	08000H,A14
	MOVE	A14,*A8(OXVEL),L
	RETS

A_ACCEL_RIGHT
	MOVE	*A8(OYVEL),A14,L
;	ADDI	-04000H,A14
	MOVE	A14,*A8(OYVEL),L

	MOVE	*A8(OXVEL),A14,L
	ADDI	-08000H,A14
	MOVE	A14,*A8(OXVEL),L
	RETS

**************************************************************************
*								         *
* A_CHECK_LEFT_STOP							 *
* A_CHECK_RIGHT_STOP - WAIT FOR HELMET TO STOP				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_CHECK_LEFT_STOP
	MOVE	*A8(OYVEL),A14,L
	ADDI	04000H,A14
	MOVE	A14,*A8(OYVEL),L

	MOVE	*A8(OXVEL),A14,L
	ADDI	08000H,A14
	MOVE	A14,*A8(OXVEL),L
	JRNZ	APW_STAY			;BR=STILL GOIN'
	RETS

A_CHECK_RIGHT_STOP
	MOVE	*A8(OYVEL),A14,L
	ADDI	04000H,A14
	MOVE	A14,*A8(OYVEL),L

	MOVE	*A8(OXVEL),A14,L
	ADDI	-08000H,A14
	MOVE	A14,*A8(OXVEL),L
	JRNZ	APW_STAY			;BR=STILL GOIN'
	RETS

**************************************************************************
*								         *
* A_CHECK_LEFT_SLAM							 *
* A_CHECK_RIGHT_SLAM - WAIT FOR HELMET TO SLAM				 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_CHECK_LEFT_SLAM
	MOVE	*A8(ODATA),A0,W
	MOVE	*A8(OXPOS),A14,W
	CMP	A0,A14
;	JRGE	APW_STOP2
	JRGE	APW_STOP

	MOVE	*A8(OXVEL),A1,L
	ADDI	010000H,A1
	MOVE	A1,*A8(OXVEL),L
	JRN	ACLS_YACCEL
	JRZ	ACLS_YACCEL
ACLS_YVEL
	ABS	A1
	SRL	16-5,A1
	ADDI	HELM_YVEL_TABLE,A1
	CMPI	HELM_YVEL_TABLE_END,A1
	JRLT	ACLS_GET_YVEL
	CLR	A14
	MOVE	A14,*A8(OYVEL),L
	JRUC	APW_STAY
ACLS_GET_YVEL
	MOVE	*A1,A14,L
	MOVE	A14,*A8(OYVEL),L
	JRUC	APW_STAY

ACLS_YACCEL
	MOVE	*A8(OYVEL),A14,L
	ADDI	04000H,A14
	MOVE	A14,*A8(OYVEL),L
	JRUC	APW_STAY

A_CHECK_RIGHT_SLAM
	MOVE	*A8(ODATA),A0,W
	MOVE	*A8(OXPOS),A14,W
	CMP	A0,A14
;	JRLE	APW_STOP2
	JRLE	APW_STOP

	MOVE	*A8(OXVEL),A1,L
	ADDI	-010000H,A1
	MOVE	A1,*A8(OXVEL),L
	JRNN	ACLS_YACCEL
     	JRUC	ACLS_YVEL

HELM_YVEL_TABLE
	.LONG	080000H,080000H,080000H,070000H,060000H,050000H,040000H
	.LONG	030000H,020000H,010000H,010000H,010000H
HELM_YVEL_TABLE_END

**************************************************************************
*								         *
* A_LEFT_PLAQ_WAIT  							 *
* A_RIGHT_PLAQ_WAIT - WAIT FOR PLAQUE IN POSITION			 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_LEFT_PLAQ_WAIT
	MOVE	*A8(ODATA),A0,W
	MOVE	*A8(OXPOS),A14,W
	CMP	A0,A14
;	JRLT	APW_STAY
;	JRUC	APW_STOP
	JRGE	APW_STOP

	MOVE	*A8(OXVEL),A14,L
	ADDI	[-1,0],A14
	JRZ	APW_STOP
	JRN	APW_STOP
	MOVE	A14,*A8(OXVEL),L

	JRUC	APW_STAY

A_RIGHT_PLAQ_WAIT
	MOVE	*A8(ODATA),A0,W
	MOVE	*A8(OXPOS),A14,W
	CMP	A0,A14
;	JRGT	APW_STAY
;	JRUC	APW_STOP
	JRLE	APW_STOP

	MOVE	*A8(OXVEL),A14,L
	ADDI	[1,0],A14
	JRNN	APW_STOP
	MOVE	A14,*A8(OXVEL),L

	JRUC	APW_STAY

APW_STOP
	MOVE	A0,*A8(OXPOS),W
APW_STOP2
	CLR	A14
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OYVEL),L

	MOVE	@TG_FLAG,A14,W
	DEC	A14
	MOVE	A14,@TG_FLAG,W

	JAUC	PULL_ANIM
APW_STAY
	MOVE	*A8(ANIMFRM),A14,L		;HOLD FRAME
	SUBI	020H+010H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
	RETS

**************************************************************************
*								         *
* A_SET_OXVEL0 - ANIM FUNC TO SET THE X VELOCITY OF AN OBJECT.		 *
* A8 = PTR TO OBJECT							 *
* AARG+,L = OXVEL							 *
*								         *
**************************************************************************

A_SET_OXVEL0
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(OXVEL),L
	RETS


**************************************************************************
*								         *
* A_CHECK_SWISH - ONLY MAKE "SWISH" SOUND FOR PLACK OBJECT		 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_CHECK_SWISH
	MOVE	*A8(OIMG),A14,L
	CMPI	CITYPLQ2,A14
	JRNE	ACS_DONE			;BR=NOT THE PLACK
	MOVI	S_SLIDE_SWISH_1,A4
	CALLA	do_sound
ACS_DONE
	RETS

LEFT_PLAQ_SLIDE_ANIM
;	LW	1,24
	LWL	1,AFUNC|1,A_TG_FLAG_WAIT
;	LWL	1,AFUNC|1,A_SET_OXVEL0+2,[8,0]
;	LWL	1,AFUNC|1,A_SET_OXVEL0+2,078000H
;	LWLL	1,AFUNC|1,A_SET_OXVEL0+2,0134000H
	LWLLLL	1,AFUNC|1,A_2FUNCS+6,A_CHECK_SWISH,A_SET_OXVEL0,0134000H
	LWL	1,AFUNC|1,A_LEFT_PLAQ_WAIT

RIGHT_PLAQ_SLIDE_ANIM
;	LW	1,24
	LWL	1,AFUNC|1,A_TG_FLAG_WAIT
;	LWL	1,AFUNC|1,A_SET_OXVEL0+2,[-8,0]
;	LWL	1,AFUNC|1,A_SET_OXVEL0+2,-078000H
	LWLL	1,AFUNC|1,A_SET_OXVEL0+2,-0134000H
	LWL	1,AFUNC|1,A_RIGHT_PLAQ_WAIT

**************************************************************************
*								         *
* DISPLAY_TEAM_GRAPHICS - DISPLAY TEAM LOGO, NAME, AND PLACK 		 *
* 									 *
* PASS:									 *
* NUTIN'								 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

DISPLAY_TEAM_GRAPHICS
	PUSH	A1
;	movk	1,a14				;keep our timer from appearing
;	move	a14,@scored_by,W		;until our script shuts it off

	CLR	A14
	DEC	A14
	MOVE	A14,@SLIDE_OFF_COUNT,W

	MOVE	@team1,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	[-1,0],A10
	MOVI	22,A11

	MOVI	TEAM_PLACK_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y,0],A1
	MOVK	2,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVI	BLUFRMP,A0
	CALLA	CHANGE_PAL
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y+4,60],A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	[PLACK_Y+32,105],A5
	MOVI	[PLACK_Y+44,105],A6
	MOVE	@team1,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	DTG_LEFT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
DTG_LEFT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVE	@team2,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	[1,0],A10

	MOVI	TEAM_PLACK_INIT,B0
	CALLA	EASYMAKE
	MOVE	*A8(OCTRL),A14,W
	ORI	M_FLIPH,A14
	MOVE	A14,*A8(OCTRL),W
	MOVI	[PLACK_Y,399],A1
	MOVK	2,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVI	REDFRMP,A0
	CALLA	CHANGE_PAL
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[PLACK_Y+4,339],A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	[PLACK_Y+32,301],A5
	MOVI	[PLACK_Y+44,301],A6
	MOVE	@team2,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	DTG_RIGHT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
DTG_RIGHT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVE	A10,*A8(ODATA),L
	MOVI	SLIDE_XOFF_ANIM,A1
	CALLA	STRT_ANIM
	MOVB	A11,*A8(ANIMLOOP)

	PULLQ	A1
	RETS

**************************************************************************
*								         *
* A_ODATA_TO_OXVEL							 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_ODATA_TO_OXVEL
	MOVE	*A8(ODATA),*A8(OXVEL),L
	RETS

**************************************************************************
*								         *
* A_SLIDE_OFF_WAIT - WAIT TIL OBJECT SLIDES OFF SCREEN			 *
* 									 *
* PASS:									 *
* A8 = OBJECT								 *
*								         *
**************************************************************************

A_SLIDE_OFF_WAIT
	MOVE	@minutes,A14,W
	SUBK	3,A14
	JRNZ	ASOW_DEL			;BR=FUCK UP?

	MOVE	@SLIDE_OFF_COUNT,A14,W
	JRN	ASOW_DONE			;BR=DON'T SLIDE YET

	MOVB	*A8(ANIMLOOP),A14
	DEC	A14
	MOVB	A14,*A8(ANIMLOOP)
	JRZ	ASOW_DEL			;BR=OFF SCREEN
	JRN	ASOW_DEL			;BR=OFF SCREEN

	MOVE	*A8(ODATA),A0,L			;X ACCEL
	MOVE	*A8(OXVEL),A1,L
	ADD	A0,A1
	MOVE	A1,*A8(OXVEL),L
ASOW_DONE
	RETS
ASOW_DEL
	MOVE	@SLIDE_OFF_COUNT,A14,W
	DEC	A14
	JRNN	ASOW_SET_COUNT			;BR=COUNT OKAY
	CLR	A14				;WHAT THE FUCK HAPPENED?
ASOW_SET_COUNT
	MOVE	A14,@SLIDE_OFF_COUNT,W

	JAUC	DELETE_OBJ			;BR=OBJECT SLID OFF, DELETE


SLIDE_XOFF_ANIM
	LWL	1,AFUNC|1,A_SLIDE_OFF_WAIT
	.long	0

;A_CLEAR_SCORED_BY
;	clr	a0
;	move	a0,@scored_by,W
;	rets

TEAM_PLACK_INIT
	.long	CITYPLQ2
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE
	.word	OM_OID
	.word	OID_SELECT_1

BIG_GHELMET_INIT
	.long	WMASKB08
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE
	.word	OM_OID
	.word	OID_SELECT_1

HELM_YSLIDE_ANIM
	LWL	1,AFUNC|1,A_YSLIDE_WAIT
	LW	WMASKB07,4
	LW	WMASKB06,4
	LW	WMASKB05,4
	LW	WMASKB04,4
	LW	WMASKB03,4
	LW	WMASKB02,4
;	LWLL	WMASKC01,AFUNC|4,A_SET_OXVEL+2,[1,0]
	LWLL	WMASKC01,AFUNC|4,A_SET_OXVEL+2,018000H
	LW	WMASKC02,4
	LW	WMASKC03,4
;	LWLLLL	WMASKD01,AFUNC|1,A_2FUNCS+6,A_SET_OXVEL,[0,0],PULL_ANIM
	LWL	WMASKD01,AFUNC|1,A_YSLIDE_DONE

LEFT_HELM_ANIM

;	LWLLLLL	WMASKC01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[-5,0],A_SET_OYVEL,[0,0]
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	WMASKC02,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	WMASKC03,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_ACCEL_LEFT
;	LWL	WMASKD01,AFUNC|1,A_ACCEL_LEFT
;	LWL	1,AFUNC|1,A_CHECK_LEFT_STOP
;	LW	1,30

;	LWLLLLL	WMASKD01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[-6,0],A_SET_OYVEL,[-3,0]
	LWLLLLL	1,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[-8,0],A_SET_OYVEL,[-4,0]

	LWL	1,AFUNC|1,A_CHECK_LEFT_STOP

	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
	LW	WMASKD01,3
	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
	LW	WMASKD01,3
	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
;	LW	WMASKD01,1

;	LWLL	WMASKD01,AFUNC|1,A_SET_OXVEL0+2,[-2,0]
	LWLLLLL	WMASKD01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[-4,0],A_SET_OYVEL,[-2,0]
	LWL	1,AFUNC|1,A_CHECK_LEFT_SLAM

RIGHT_HELM_ANIM

;	LWLLLLL	WMASKC01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[6,0],A_SET_OYVEL,[0,0]
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	WMASKC02,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	WMASKC03,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_ACCEL_RIGHT
;	LWL	WMASKD01,AFUNC|1,A_ACCEL_RIGHT
;	LWL	1,AFUNC|1,A_CHECK_RIGHT_STOP
;	LW	1,30

;	LWLLLLL	WMASKD01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[6,0],A_SET_OYVEL,[-3,0]
	LWLLLLL	1,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[8,0],A_SET_OYVEL,[-4,0]

	LWL	1,AFUNC|1,A_CHECK_RIGHT_STOP

	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
	LW	WMASKD01,3
	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
	LW	WMASKD01,3
	LW	WMASKD02,3
	LW	WMASKD03,2
	LW	WMASKD04,1
;	LW	WMASKD01,1

;	LWLL	WMASKD01,AFUNC|1,A_SET_OXVEL0+2,[2,0]
	LWLLLLL	WMASKD01,AFUNC|1,A_2FUNCS+8,A_SET_OXVEL0+2,[4,0],A_SET_OYVEL,[-2,0]
	LWL	1,AFUNC|1,A_CHECK_RIGHT_SLAM

GROUP_LIGHTS_INIT
	.long	BXLT01
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE|M_NOVEL
	.word	OM_OID|OM_ANIM
	.word	OID_SELECT_1
	.long	GROUP_LIGHT_ANIM

GROUP_LIGHT_ANIM
	LWL	1,AFUNC|1,A_TG_FLAG_WAIT
	LW	BXLT02,4
	LW	BXLT03,4
	LW	BXLT04,4
	LW	BXLT05,4
	LW	BXLT06,4
	LW	BXLT07,4
	LWL	BXLT08,AFUNC|1,PULL_ANIM

CENTER_LIGHT_INIT
	.byte	 4, 0, 0, 1	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	LTS0001P	;IMGPAL
	.word	OID_SELECT_1, 0	;OID, GLOBAL_OPTIONS
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	LTS0007, DMAWNZ, M_SCRNOBJ|M_NOSCALE|M_NOVEL, 0, OM_ANIM
	.LONG	CENTER_LIGHT_UP_ANIM
	LWWWW	LTS0007, DMAWNZ|M_FLIPH, M_SCRNOBJ|M_NOSCALE|M_NOVEL, 0, OM_PRVANIM
	LWWWW	LTS0007, DMAWNZ|M_FLIPH|M_FLIPV, M_SCRNOBJ|M_NOSCALE|M_NOVEL, 0, OM_PRVANIM
	LWWWW	LTS0007, DMAWNZ|M_FLIPV, M_SCRNOBJ|M_NOSCALE|M_NOVEL, 0, OM_PRVANIM

CENTER_LIGHT_UP_ANIM
	LWL	1,AFUNC|1,A_TG_FLAG_WAIT
	LW	LTS0008,3
	LW	LTS0009,3
	LW	LTS0010,3
	LW	LTS0011,3
	LW	LTS0012,4
	LW	LTS0013,4
	LW	LTS0014,4
	LWL	LTS0015,AFUNC|1,PULL_ANIM

CENTER_LIGHT_DIM_ANIM
	LW	LTS0014,4
	LW	LTS0013,4
	LW	LTS0012,4
	LW	LTS0011,4
	LW	LTS0010,4
	LW	LTS0009,4
	LW	LTS0008,4
	LW	LTS0007,4
	LWL	1,AFUNC|1,DELETE_OBJ

NHL_LOGO_INIT
	.long	BLOGO101
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE|M_NOVEL
	.word	OM_OID|OM_POS|OM_INSERT
	.word	OID_SELECT_1
	.long	[199,0],[NHL_LOGO_Y,0],4

NHL_LOGO_SPIN_ANIM
	LWLL	BLOGO101,AFUNC|1,A_CHANGE_PAL+2,BLOGO101P
	LW	BLOGO102,2
	LW	BLOGO103,2
	LW	BLOGO104,2
	LW	BLOGO105,2
	LW	BLOGO106,2
	LW	BLOGO107,2
	LW	BLOGO108,2
	LWLL	BLOGO201,AFUNC|2,A_CHANGE_PAL+2,BLOGO201P
	LW	BLOGO202,2
	LW	BLOGO203,2
	LW	BLOGO204,2
	LW	BLOGO205,2
	LW	BLOGO206,2
	LW	BLOGO207,2
	LWL	BLOGO208,AFUNC|1,PULL_ANIM

NHL_DEL_ANIM
;	LW	1,2
;	LW	1,2
	LW	1,2
	LWL	1,AFUNC|1,DELETE_OBJ

EXPLO_INIT
	.long	BGBRST03
	.word	DMAWNZ,M_SCRNOBJ|M_NOSCALE|M_NOVEL
	.word	OM_OID|OM_POS|OM_ANIM|OM_INSERT
	.word	OID_SELECT_1
	.long	EXPLO_ANIM
	.long	[199,0],[NHL_LOGO_Y,0],0

EXPLO_ANIM
	LW	1,2
;	LW	BGBRST02,2
;	LW	BGBRST03,2
	LW	BGBRST04,2
	LW	BGBRST05,2
	LW	BGBRST06,2
	LW	BGBRST07,2
	LW	BGBRST08,2
	LW	BGBRST09,2
	LW	BGBRST10,2
	LW	BGBRST11,2
	LW	BGBRST12,2
	LW	BGBRST13,2
	LW	BGBRST14,2
	LW	BGBRST15,2
	LW	BGBRST16,2
	LW	BGBRST17,2
	LW	BGBRST18,2
	LW	BGBRST19,2
	LW	BGBRST20,2
	LW	BGBRST21,2
	LW	BGBRST22,2
	LW	BGBRST23,2
	LW	BGBRST24,2
	LW	BGBRST25,2
	LW	BGBRST26,2
	LWL	1,AFUNC|1,DELETE_OBJ

TONIGHTS_GAME_MESS
;	MESS_MAC RD20FONT,3,200,16-6,COLOR_PBLACK,STRCNRMO,0
	MESS_MAC2 H18FONT,-1,0,200,20,HFPAL,STRCNCONV,0
	.STRING	"TONIGHT'S\n\fGAME",0
	.LONG	[-8,0]
	.EVEN

	.END
